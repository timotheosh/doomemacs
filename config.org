#+title: Emacs Config (Doom Emacs)
#+author: Tim Hawes
#+property: header-args :tangle yes :mkdirp yes

Place your private configuration here! Remember, you do not need to run ~doom sync~ after modifying this file!
* Packages File
Create the packages.el file.
#+BEGIN_SRC emacs-lisp :tangle packages.el
;; -*- no-byte-compile: t; -*-
;;; $DOOMDIR/packages.el
#+END_SRC

* Heading
#+BEGIN_SRC emacs-lisp
;;; $DOOMDIR/config.el -*- lexical-binding: t; -*-

#+END_SRC
* Identification
Some functionality uses this to identify you, e.g. GPG configuration, email clients, file templates and snippets.
#+BEGIN_SRC emacs-lisp
(setq user-full-name "Tim Hawes"
      user-mail-address "trhawes@gmail.com")
#+END_SRC
* Fonts
Doom exposes five (optional) variables for controlling fonts in Doom. Here are the three important ones:
- ~doom-font~
- ~doom-variable-pitch-font~
- ~doom-big-font~ -- used for ~doom-big-font-mode~ Use this for presentations or streaming.
They all accept either a font-spec, font string ("Input Mono-12"), or xlfd font string. You generally only need these two:
#+BEGIN_SRC emacs-lisp
(setq doom-font (font-spec :family "monospace" :size 18))
#+END_SRC
* Theme
There are two ways to load a theme. Both assume the theme is installed and available. You can either set ~doom-theme~ or manually load a theme with the ~load-theme~ function. This is the default:
#+BEGIN_SRC emacs-lisp
(setq doom-theme 'doom-one)
#+END_SRC
* Line number style
This determines the style of line numbers in effect. If set to `nil', line numbers are disabled. For relative line numbers, set this to `relative'.
#+BEGIN_SRC emacs-lisp
(setq display-line-numbers-type t)
#+END_SRC
* Additional Macros
Here are some additional functions/macros that could help you configure Doom:
- ~load!~ for loading external *.el files relative to this one
- ~use-package~ for configuring packages
- ~after!~ for running code after a package has loaded
- ~add-load-path!~ for adding directories to the `load-path', relative to this file. Emacs searches the ~load-path~ when you load packages with ~require~ or ~use-package~.
- ~map!~ for binding new keys

To get information about any of these functions/macros, move the cursor over the highlighted symbol at press 'K' (non-evil users must press 'C-c g k'). This will open documentation for it, including demos of how they are used.

You can also try 'gd' (or 'C-c g d') to jump to their definition and see how they are implemented.
* Keys
** Centaur Tabs
#+BEGIN_SRC emacs-lisp
(map! :map centaur-tabs-mode-map
      :g "C-<prior>" #'centaur-tabs-backward
      :g "C-<next>" #'centaur-tabs-forward)
#+END_SRC
** Projectile hotkeys
#+BEGIN_SRC emacs-lisp
(map! :map projectile-mode-map
      :g "<f4>" #'projectile-find-file)
#+END_SRC
** Return counsel-find-file back to its default behavior
#+BEGIN_SRC emacs-lisp
(after! ivy
  (map! :map ivy-minibuffer-map
        :g "TAB" #'ivy-partial))
(after! counsel
  (map! :map counsel-M-x-map
        :g "TAB" #'ivy-partial))
#+END_SRC
** Multi-term
#+BEGIN_SRC emacs-lisp
(map! "C-M-<right>" 'multi-term-next
      "C-M-<left>" 'multi-term-prev)
#+END_SRC
** Shell-pop
#+BEGIN_SRC emacs-lisp
(map! "<f3>" '+eshell/toggle)
#+END_SRC
* Emacs Frame Manager
The purpose of this modules is managing Emacs windows in an environment without using EXWM. This will offer functions an emacsclient can run conditioned on the current state of the window, and fast terminal access within Emacs. This will work with X11, not sure what the implications are for Cocoa or Windows.
** Setup
We'll need to load this file.
#+BEGIN_SRC emacs-lisp
(load! "+emacs-frame-manager")
#+END_SRC
Setup the heading for this file
#+BEGIN_SRC emacs-lisp :tangle +emacs-frame-manager.el
;; Copyright (C) 2020 Tim Hawes <thawes@gmail.com>
;; Maintainer: Tim Hawes
;; Created: 15 May 2020
;; Version: 0.01
#+END_SRC

** Frame Names
First, we set up unique names for the X Window names, so we can easily reference these windows in an X Window environment. The names have random numbers, to make them easier to isolate among many windows in an X environment.
   #+BEGIN_SRC emacs-lisp :tangle +emacs-frame-manager.el
(defvar efm/frame-name "emacs-frame-manager998")
(defvar efm/shell-name "emacs-frame-manager336")
(defvar efm/org-name "emacs-frame-manager920")
   #+END_SRC
** Extra frames
When emacs runs in daemon mode under systemd, emacsclient can, and sometimes will, create extra frames when you execute a command with emacsclient that does not need a frame, before any frames have been opened, and then execute emacsclient with a new frame. We keep track of legitimate frames, so we can just delete the unneeded frames. If you add new frames above that you intend to use, be sure to add them to this list, so they do not get inadvertently deleted.
   #+BEGIN_SRC emacs-lisp :tangle +emacs-frame-manager.el
(defvar efm/legit-frames (list efm/frame-name efm/shell-name efm/org-name "F1"))
   #+END_SRC
- Now the utility functions
  #+BEGIN_SRC emacs-lisp :tangle +emacs-frame-manager.el
(defun efm/list-illegite-frames ()
  "Lists visible illegitimate frames. Essentially all frames not in the efm/legit-frames list and is visible."
  (remove-if
   (lambda (x)
     (seq-find (lambda (y)
                 (string= y
                          (frame-parameter x 'name))) efm/legit-frames))
   (remove-if-not 'frame-visible-p (frame-list))))

(defun efm/kill-illegite-frames ()
  "Deletes the extra visible frames."
  (dolist (buf (efm/list-illegite-frames))
    (delete-frame buf)))
  #+END_SRC
** Frame management
Utility functions for frame management. These find frames, suspend frames, raise frames and maximize frames.
#+BEGIN_SRC emacs-lisp :tangle +emacs-frame-manager.el

(defun efm/find-frame (frame-name)
  "Returns a list of frames with frame-name."
  (remove-if-not
   (lambda (x)
     (string= (frame-parameter x 'name) frame-name))
   (frame-list)))

(defun efm/maximized-p (frame)
  "Returns true if frame is maximized or fullboth."
  (cdr (assoc 'fullscreen (frame-parameters frame))))

(defun efm/create-frame (frame-name frame-title)
  "Creates a maximized frame, raised and in focus."
  (make-frame-on-display (getenv "DISPLAY") `((name . ,frame-name)
                                              (title . ,frame-title)
                                              (fullscreen . maximized)
                                              (window-system . x)))
  (let ((frame (car (efm/find-frame name))))
    (frame-focus frame)
    (x-focus-frame frame)))

(defun efm/raise-frame (frame)
  "Raises a frame and puts it in focus."
  (raise-frame frame)
  (select-frame frame)
  (x-focus-frame frame))

(defun efm/frame-focus-maximize (frame &optional command)
  "Raise, focus, and maximize a frame."
  (efm/raise-frame frame)
  (modify-frame-parameters frame '((fullscreen . maximized)))
  (when command
    (eval (list (intern command)))))

(defun efm/start-client-with-command (name title &optional command skip-taskbar)
  "Create a new frame, executing command."
  (efm/create-frame name title)
  (if command
      (eval (list (intern command)))
    (switch-to-buffer "*dashboard*"))
  (when skip-taskbar
    (modify-frame-parameters (car (efm/find-frame name)) '((skip-taskbar t)
                                                           (undecorated t)))))

(defun efm/raise-or-start (name title &optional command toggle skip-taskbar)
  "If frame with name does not exist, create it, otherwise raise, focus and maximize the existing frame."
  (let ((frame (car (efm/find-frame name))))
    (if frame
        (if (and (frame-focus-state frame)
                 (efm/maximized-p frame)
                 (or (and (null command) (null toggle))
                     (and (not (null command)) (not (null toggle)))))
            (progn (select-frame frame)
                   (suspend-frame))
          (efm/frame-focus-maximize frame command))
      (efm/start-client-with-command name title command skip-taskbar))))

#+END_SRC
* Email
Use Gmail in gnus
** Settings
#+BEGIN_SRC emacs-lisp :tangle +email.el
(setq!
 send-mail-function 'smtpmail-send-it
 message-send-mail-function 'smtpmail-send-it
 user-mail-address "trhawes@gmail.com"
 smtpmail-starttls-credentials '(("smtp.gmail.com" "587" nil nil))
 smtpmail-auth-credentials (expand-file-name "~/.authinfo")
 smtpmail-default-smtp-server "smtp.gmail.com"
 smtpmail-smtp-server "smtp.gmail.com"
 smtpmail-smtp-service 587
 smtpmail-debug-info t
 starttls-extra-arguments nil
 starttls-gnutls-program "/usr/bin/gnutls-cli"
 starttls-extra-arguments nil
 starttls-use-gnutls t
 )
#+END_SRC
** Load Email settings
#+BEGIN_SRC emacs-lisp
(load! "+email")
#+END_SRC
* Web browser
** Settings
#+BEGIN_SRC emacs-lisp
(setq! browse-url-generic-program "/usr/local/bin/next")
(setq! browse-url-default-browser 'eww-browse-url)
;;(setq shr-external-browser 'browse-url-generic)
(setq!
 browse-url-browser-function
 '(
   ("youtube\\.com" . browse-url-generic)
   ("vimeo\\.com" . browse-url-generic)
   ("facebook\\.com" . browse-url-firefox)
   ("reddit\\.com" . browse-url-firefox)
   ("." . eww-browse-url)))
#+END_SRC
* Dired
** Packages
#+BEGIN_SRC emacs-lisp :tangle packages.el
(package! ranger)
#+END_SRC
** Settings
#+BEGIN_SRC emacs-lisp :tangle +dired
(add-hook 'dired-mode-hook (lambda ()
                             (setq dired-hide-details-mode t)
                             (setq ranger-override-dired-mode t)))
#+END_SRC
** Load Dired settings
#+BEGIN_SRC emacs-lisp
(load! "+dired")
#+END_SRC
* Eshell
** Packages
*** [[https://github.com/tom-tan/esh-help][esh-help]] for Eshell help
#+BEGIN_SRC emacs-lisp :tangle packages.el
(package! esh-help)
#+END_SRC
#+BEGIN_SRC emacs-lisp
(after! eshell
  (use-package! esh-help)
  (setup-esh-help-eldoc))
#+END_SRC
*** [[https://github.com/emacsmirror/multi-eshell][Multiple eshell]] Original blog seems to be missing, but available on marmalade.
#+BEGIN_SRC emacs-lisp :tangle packages.el
(package! multi-eshell)
#+END_SRC
#+BEGIN_SRC emacs-lisp
(use-package! multi-eshell)
#+END_SRC
*** [[https://github.com/porterjamesj/virtualenvwrapper.el][Virtualenvwrapper]] for Emacs
[[https://virtualenvwrapper.readthedocs.io/en/latest/][Virtualenvwrapper]] is a set of extensions for more easily managing multiple virtualenv's for Python. It is available on Debian and Ubuntu systems. This is an Emacs module that interfaces with that system, making it easy to use in Eshell and Emacs proper.
#+BEGIN_SRC emacs-lisp :tangle packages.el
(package! virtualenvwrapper)
#+END_SRC
#+BEGIN_SRC emacs-lisp
(use-package! virtualenvwrapper)
(setq! venv-location "~/.virtualenvs/")
#+END_SRC
** Settings
*** Custom magit commands in eshell
#+BEGIN_SRC emacs-lisp :tangle +eshell.el
(defun eshell/mgit (&rest args)
  "Using magit in eshell"
  (eshell-eval-using-options
   "mgit" args
   '((?s "status" nil status "Show git status for repo.")
     (?l "log" nil log "Show git log for all branches")
     (nil "help" nil nil "Show this usage information")
     :show-usage)
   (eshell-do-eval
    (eshell-parse-command
     (cond
      (status "magit-status")
      (log "magit-log-all-branches")))
    t)))
#+END_SRC
*** Custom dpkg commands in eshell
#+BEGIN_SRC emacs-lisp :tangle +eshell.el
(defun eshell/deb (&rest args)
  "deb command for eshell"
  (eshell-eval-using-options
   "deb" args
   '((?f "find" t find "list available packages matching a pattern")
     (?i "installed" t installed "list installed debs matching a pattern")
     (?l "list-files" t list-files "list files of a package")
     (?s "show" t show "show an available package")
     (?v "version" t version "show the version of an installed package")
     (?w "where" t where "find the package containing the given file")
     (nil "help" nil nil "show this usage information")
     :show-usage)
   (eshell-do-eval
    (eshell-parse-command
     (cond
      (find
       (format "apt-cache search %s" find))
      (installed
       (format "dlocate -l %s | grep '^.i'" installed))
      (list-files
       (format "dlocate -L %s | sort" list-files))
      (show
       (format "apt-cache show %s" show))
      (version
       (format "dlocate -s %s | egrep '^(Package|Status|Version):'" version))
      (where
       (format "dlocate %s" where))))
    t)))
#+END_SRC
*** Eshell history settings
#+BEGIN_SRC emacs-lisp :tangle +eshell.el
(setq eshell-history-size 1024)

(load "em-hist")           ; So the history vars are defined
(if (boundp 'eshell-save-history-on-exit)
    (setq eshell-save-history-on-exit t)) ; Don't ask, just save
                                        ;(message "eshell-ask-to-save-history is %s" eshell-ask-to-save-history)
(if (boundp 'eshell-ask-to-save-history)
    (setq eshell-ask-to-save-history 'always)) ; For older(?) version
                                        ;(message "eshell-ask-to-save-history is %s" eshell-ask-to-save-history)
#+END_SRC
*** Tramp settings for eshell
#+BEGIN_SRC emacs-lisp :tangle +eshell.el
(require 'esh-module) ;; load tramp functions into eshell
#+END_SRC
*** Custom prompt
#+BEGIN_SRC emacs-lisp :tangle packages.el
(package! eshell-prompt-extras)
#+END_SRC
#+BEGIN_SRC emacs-lisp :tangle +eshell.el
(use-package! eshell-prompt-extras
  :config
  ;; for virtualenvwrapper stuff
  (with-eval-after-load "esh-opt"
    (require 'virtualenvwrapper)
    (venv-initialize-eshell)
    (autoload 'epe-theme-lambda "eshell-prompt-extras")
    (setq eshell-highlight-prompt nil
          eshell-prompt-function 'epe-theme-lambda
          eshell-prompt-regexp "^[^#\nλ]*[#λ] "
          epe-show-python-info t
          epe-path-style 'single)))
#+END_SRC
** Modules
#+BEGIN_SRC emacs-lisp :tangle +eshell.el
(add-to-list 'eshell-modules-list 'eshell-tramp 'esh-opt)
#+END_SRC
** Preferred functions and variables
#+BEGIN_SRC emacs-lisp :tangle +eshell.el
(setq eshell-prefer-lisp-functions t)
(setq eshell-prefer-lisp-variables t)
#+END_SRC
** Password caching
#+BEGIN_SRC emacs-lisp :tangle +eshell.el
(setq password-cache t) ; enable password caching
(setq password-cache-expiry 300) ; for 5 minutes (time in secs)
#+END_SRC
** Progress bar for apt in minibuffer
#+BEGIN_SRC emacs-lisp :tangle +eshell.el
;; Progress bars, like apt in the status/echo area
(advice-add
 'ansi-color-apply-on-region
 :before 'ora-ansi-color-apply-on-region)

(defun ora-ansi-color-apply-on-region (begin end)
  "Fix progress bars for e.g. apt(8).
Display progress in the mode line instead."
  (let ((end-marker (copy-marker end))
        mb)
    (save-excursion
      (goto-char (copy-marker begin))
      (while (re-search-forward "\0337" end-marker t)
        (setq mb (match-beginning 0))
        (when (re-search-forward "\0338" end-marker t)
          (ora-apt-progress-message
           (substring-no-properties
            (delete-and-extract-region mb (point))
            2 -2)))))))

(defun ora-apt-progress-message (progress)
  (message
   (replace-regexp-in-string
    "%" "%%"
    (ansi-color-apply progress))))
#+END_SRC
** Visual commands
#+BEGIN_SRC emacs-lisp :tangle +eshell.el
;; Visual commands
;; defaults are ("vi" "screen" "top" "less" "more" "lynx" "ncftp" "pine" "tin" "trn" "elm")
(setq eshell-visual-commands '("vi" "screen" "top" "less" "more" "lynx" "ncftp" "pine" "tin" "trn" "elm"))
(dolist (cmd '("tmux" "aptitude" "aws-shell" "neofetch" "htop"))
  (add-to-list 'eshell-visual-commands cmd))
#+END_SRC
** Use Emacs completion package for Eshell
#+BEGIN_SRC emacs-lisp :tangle +eshell.el
;; Uses the default Emacs completion package for tab-complete in eshell.
(add-hook 'eshell-mode-hook
          (lambda ()
            (define-key eshell-mode-map (kbd "<tab>")
              (lambda () (interactive) (pcomplete-std-complete)))))
#+END_SRC
** Load Eshell Settings
#+BEGIN_SRC emacs-lisp
(load! "+eshell.el")
#+END_SRC
* Org-mode Files
If you use `org' and don't want your org files in the default location below, change `org-directory'. It must be set before org loads!
#+BEGIN_SRC emacs-lisp
(setq org-directory "~/org/")
#+END_SRC
* Org-mode
#+BEGIN_SRC emacs-lisp
(setq org-startup-folded t)
#+END_SRC
* Programming Languages
** General
*** Packages
- [[https://github.com/Malabarba/aggressive-indent-mode][Aggressive indent]] for better formatting of code.
  #+BEGIN_SRC emacs-lisp :tangle packages.el
  (package! aggressive-indent)
  #+END_SRC
- [[https://github.com/company-mode/company-quickhelp][Company-quickhelp]] for on the fly documentation.
  #+BEGIN_SRC emacs-lisp :tangle packages.el
  (package! company-quickhelp)
  #+END_SRC
*** Settings
**** Code folding
#+BEGIN_SRC emacs-lisp :tangle packages.el
(package! origami)
#+END_SRC
#+BEGIN_SRC emacs-lisp
(use-package! origami
  :bind (("C-<tab>" . origami-recursively-toggle-node)
           ("C-<iso-lefttab>" . origami-toggle-all-nodes))
  :hook 'prog-mode-hook)
#+END_SRC
**** Company-quickhelp
#+BEGIN_SRC emacs-lisp
(after! company
  (setq! company-quickhelp-delay 0.2))
(map! :map company-active-map
      :g "C-c h" #'company-quickhelp-manual-begin)
#+END_SRC
**** Smartparens confirguration
#+BEGIN_SRC emacs-lisp
(use-package! smartparens-config
  :config
  ;; For lisp modes
  (sp-with-modes sp--lisp-modes
    ;; disable ', it's the quote character!
    (sp-local-pair "'" nil :actions nil)
    ;; also only use the pseudo-quote inside strings where it serve as
    ;; hyperlink.
    (sp-local-pair "`" "'" :when '(sp-in-string-p sp-in-comment-p))
    (sp-local-pair "`" nil
                   :skip-match
                   (lambda (ms mb me)
                     (cond
                      ((equal ms "'")
                       (or (sp--org-skip-markup ms mb me)
                           (not (sp-point-in-string-or-comment))))
                      (t (not (sp-point-in-string-or-comment))))))))
#+END_SRC
**** Match parenthesis/brackets
#+BEGIN_SRC emacs-lisp
(defun my/match-paren (arg)
  "Go to the matching paren if on a paren; otherwise insert normally."
  (interactive "p")
  (cond ((looking-at "\\s\(") (forward-list 1) (backward-char 1))
        ((looking-at "\\s\)") (forward-char 1) (backward-list 1))
        (t (self-insert-command (or arg 1)))))
(map! :map prog-mode-map
      :g "<backtab>" 'my/match-paren)
#+END_SRC
**** Lisp extra fontlock
#+BEGIN_SRC emacs-lisp :tangle packages.el
(package! lisp-extra-font-lock)
#+END_SRC
#+BEGIN_SRC emacs-lisp
(use-package! lisp-extra-font-lock)
#+END_SRC
** Common Lisp
*** Settings
#+BEGIN_SRC emacs-lisp :tangle +common-lisp.el
(use-package! aggressive-indent)
(use-package! company-quickhelp)
(add-hook! 'lisp-mode-hook
           #'smartparens-strict-mode
           #'aggressive-indent-mode
           #'lisp-extra-font-lock-mode
           #'company-quickhelp-mode)
(after! sly
  (setq! sly-lisp-implementations
         '((sbcl ("~/programs/bin/ros" "-L" "sbcl" "-Q" "run") :coding-system utf-8-unix)
           (clisp ("~/programs/bin/ros" "-L" "clisp" "-Q" "run"))
           (clozure-cl ("~/programs/bin/ros" "-L" "ccl-bin" "-Q" "run"))
           (cmucl ("~/programs/bin/ros" "-L" "cmu-bin" "-Q" "run"))
           (ecl ("~/programs/bin/ros" "-L" "ecl" "-Q" "run") :coding-system utf-8-unix)
           (abcl ("~/programs/bin/ros" "-L" "abcl-bin" "-Q" "run"))))
  (add-hook! 'sly-mrepl-hook #'company-quickhelp-mode))
#+END_SRC
**** Hyperspec lookup
Open CL REPL and execute: ~(ql:quickload "clhs")~, then follow instructions.
~C-c C-d h~ on common lisp directive, and it should open the definition in the default web browser.
#+BEGIN_SRC emacs-lisp
(load! "/home/thawes/.roswell/lisp/quicklisp/clhs-use-local.el")
(map! :after sly
      :map lisp-mode-map
      :g "C-c C-d h" #'sly-documentation-lookup)
#+END_SRC
**** Common Lisp Language Server
This is functional, but untested on Doom Emacs, and disabled for now. Most of the functionality for this is given with Sly/Slime.

In order to use, be sure to install the language server first, by running ~ros install cxxxr/cl-lsp~
See also the Github repo [[https://github.com/cxxxr/cl-lsp.git][cl-lsp]].
#+BEGIN_SRC emacs-lisp
;; (add-to-list 'lsp-language-id-configuration '(lisp-mode "lisp"))
;;   (lsp-register-client
;;    (make-lsp-client :new-connection (lsp-stdio-connection "cl-lsp")
;;                     :major-modes '(lisp-mode)
;;                     :server-id 'cl-lsp))
;;   (add-hook 'lisp-mode-hook 'lsp-deferred)
#+END_SRC
*** Load
#+BEGIN_SRC emacs-lisp
(load! "+common-lisp")
#+END_SRC
** Emacs Lisp
*** Settings
#+BEGIN_SRC emacs-lisp :tangle +emacs-lisp.el
(after! emacs-lisp
  (add-to-list 'company-backends 'company-elisp))
(add-hook! 'emacs-lisp-mode-hook
           #'eldoc-mode
           #'smartparens-strict-mode
           #'aggressive-indent-mode
           #'lisp-extra-font-lock-mode
           #'company-quickhelp-mode)
#+END_SRC
*** Load Settings
#+BEGIN_SRC emacs-lisp
(load! "+emacs-lisp.el")
#+END_SRC
** Python
*** Set Interpreter
#+BEGIN_SRC emacs-lisp
(setq python-shell-interpreter "ipython")
#+END_SRC
